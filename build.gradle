import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id "org.openapi.generator" version "4.3.1"
	id 'jacoco'
}

group = 'edu.bbte.projectblueblook'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

apply from: "$rootDir/lint.gradle"

repositories {
	mavenCentral()
}

dependencies {
	// String dependencies
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.4.2'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.4.2'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb-reactive', version: '2.4.2'

	// Model and mapper dependencies
	compile group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
	compile group: 'org.mapstruct', name: 'mapstruct', version: '1.4.1.Final'
	annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.4.1.Final'

	// OpenAPI dependencies
	compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.8.0'
	compile group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.1.0'

	// Others
	// TODO: sort dependencies out
	compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.3'
	compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
	compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.11'
	compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.4.0-b180830.0359'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'

	// JWT dependencies
	compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'

	// Test dependencies
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.6.28'
	compile group: 'org.jacoco', name: 'org.jacoco.core', version: '0.8.6'

}

bootRun {
	jvmArgs = ["-Djdk.tls.client.protocols=TLSv1.2"]
}

jacoco {
	toolVersion = "0.8.5"
	reportsDir = file("$buildDir/jacoco")
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

task fatJar(type: Jar) {
	manifest {
		attributes 'Main-Class': 'edu.bbbte.idde.projectbluebook.datacatalog.users.DatacatalogApplication'
	}
	baseName = 'all-in-one-jar'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}


openApiGenerate {
	inputSpec = "$rootDir/src/main/resources/api/dist.yaml".toString()
	configFile = "$rootDir/openapi-generator-config.json".toString()
	ignoreFileOverride = "$rootDir/.openapi-generator-ignore".toString()
	outputDir = "$rootDir".toString()
	generatorName = "spring"
	validateSpec = false
	configOptions = [
			hideGenerationTimestamp: "true"
	]
}

tasks.withType(Test) {
	testLogging {
		// set options for log level LIFECYCLE
		events TestLogEvent.FAILED,
				TestLogEvent.PASSED,
				TestLogEvent.SKIPPED,
				TestLogEvent.STANDARD_OUT
		exceptionFormat TestExceptionFormat.FULL
		showExceptions true
		showCauses true
		showStackTraces true

		// set options for log level DEBUG and INFO
		debug {
			events TestLogEvent.STARTED,
					TestLogEvent.FAILED,
					TestLogEvent.PASSED,
					TestLogEvent.SKIPPED,
					TestLogEvent.STANDARD_ERROR,
					TestLogEvent.STANDARD_OUT
			exceptionFormat TestExceptionFormat.FULL
		}
		info.events = debug.events
		info.exceptionFormat = debug.exceptionFormat

		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
				def startItem = '|  ', endItem = '  |'
				def repeatLength = startItem.length() + output.length() + endItem.length()
				println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
			}
		}
	}
}